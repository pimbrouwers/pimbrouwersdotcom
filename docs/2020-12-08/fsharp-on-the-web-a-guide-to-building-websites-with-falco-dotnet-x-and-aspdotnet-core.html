<!DOCTYPE html>
<html lang="en">

<!-- /head -->
<head>
<meta charset="utf-8">
<meta content="width=device-width,initial-scale=1" name="viewport">
<meta content="IE=edge,chrome=1" http-equiv="X-UA-Compatible">
<title>F# on the Web: A guide to building websites with Falco, .NET 5.x and ASP.NET Core</title>

<!-- /head/style -->
<link rel="icon" href="/favicon.svg">
<link href="/prism.css" rel="stylesheet" />
<style>
:root {
  --sepia: rgb(254, 252, 251);
  --silver: rgb(204, 204, 204);
  --slate: rgb(51, 51, 51);
  --purple: rgb(184, 69, 252);
  --washed-purple: rgba(185, 69, 252, 0.102);
}
html { max-width: 48rem; margin: 1rem; font-size: 16px; font-family: Georgia, serif; line-height: 1.5; color: var(--slate); background: var(--sepia); }
hr { max-width: 80px; height: 0; margin: 2rem 0; border-top: 1px dotted var(--silver); }
a { color: inherit; }
p, p > a, p > a:visited { color: inherit; }
p > a:hover { color: var(--purple); }
blockquote { margin-left: 0; margin-right: 0; padding: 1em; border-left: 4px solid var(--purple); background: var(--washed-purple); }
img { max-width: 100%; margin-top: .5rem; }
.big { font-size: 125%; }
.muted { opacity: .5; }
.monospace { font-family: monospace; }

code[class*="language-"], pre[class*="language-"] { font-size: .875rem; }
pre[class*="language-"] { padding: 1rem; }
pre { overflow-x: auto; overflow-wrap: normal; white-space: pre; font-size: .875rem; margin: 1rem 0; padding: 1rem; color: #eff0f9; background: #333; }
code { vertical-align: middle; font-family: Consolas, Monaco, 'Andale Mono', 'Ubuntu Mono', monospace; background-color: var(--washed-purple); }
pre > code { background-color: inherit; }
p > code { padding: 0 .25rem; }

@media screen and (min-width: 640px) {
  html { margin: 7% 10%; }
  body { font-size: 1.125em; }
  h2 { margin: 1.75em 0 0; }
  h3 { margin: 1.75em 0 0; }
  code[class*="language-"], pre[class*="language-"] { font-size: 1rem; }
}
</style>
<!-- Global site tag (gtag.js) - Google Analytics -->
<script async src="https://www.googletagmanager.com/gtag/js?id=UA-74210946-2"></script>
<script>
  window.dataLayer = window.dataLayer || [];
  function gtag(){dataLayer.push(arguments);}
  gtag('js', new Date());

  gtag('config', 'UA-74210946-2');
</script>
</head>

<!-- /body -->
<body>
<!-- /body/post -->
<p><a href="/">« Go home</a></p>
<h1 id="f-on-the-web-a-guide-to-building-websites-with-falco.net-5.x-and-asp.net-core">F# on the Web: A guide to building websites with Falco, .NET 5.x and ASP.NET Core</h1>
<p>F# has a rich and mature development scene for both client- and server-side web programming. <a href="https://github.com/fable-compiler/Fable">Fable</a> revolutionized the durability with which you can craft client-side code, providing F#'s steadfast type system and an MVU programming model. On there server, <a href="https://github.com/giraffe-fsharp/giraffe">Giraffe</a>, <a href="https://github.com/SaturnFramework/Saturn">Saturn</a> and <a href="https://github.com/pimbrouwers/Falco">Falco</a> each offer their own unique way of providing similar durability.</p>
<hr />
<p><small class="muted monospace">DECEMBER 8, 2020</small></p>
<p>The focus of this post is going to be the <a href="https://github.com/pimbrouwers/Falco">Falco Framework</a>, where will aim to build a small, but complete-<em>ish</em> bullet journal using .NET 5.x and ASP.NET Core.</p>
<blockquote>
<p>The final solution can be found <a href="https://github.com/pimbrouwers/FalcoJournal">here</a> if you'd like to follow along that way.</p>
</blockquote>
<p>We'll be bringing in a couple of other packages to help us get the job done as well:</p>
<ul>
<li><a href="https://www.nuget.org/packages/System.Data.SQLite">System.Data.SQLite</a></li>
<li><a href="https://github.com/pimbrouwers/Donald">Donald</a></li>
<li><a href="https://github.com/pimbrouwers/Validus">Validus</a></li>
</ul>
<p>The final app will look something like:</p>
<p><img src="/img/2020-12-08-fsharp-falco/app.png" alt="&quot;FalcoJournal screenshot&quot;" /></p>
<h2 id="domain">Domain</h2>
<p>Our bullet journal has a dead simple domain, since we effectively only have one entity to concern ourselves with, a journal entry. But this is great because it will let us focus most of our energy on the web side of things.</p>
<p>To encapsulate our journal entry we'll need to know a few things, 1) the content, 2) when it was written and 3) a way to look it up in the future.</p>
<p>In F# it will look something like:</p>
<pre><code class="language-fsharp">module FalcoJournal.Domain

open System

type Entry =
    { EntryId      : int
      HtmlContent  : string
      TextContent  : string
      EntryDate    : DateTime }
</code></pre>
<p>With the definition taking a similar shape in SQL:</p>
<pre><code class="language-sql">CREATE TABLE entry (
    entry_id       INTEGER  NOT NULL  PRIMARY KEY
  , html_content   TEXT     NOT NULL
  , text_content   TEXT     NOT NULL
  , entry_date     TEXT     NOT NULL
  , modified_date  TEXT     NOT NULL);
</code></pre>
<blockquote>
<p>Notice the addition of <code>modified_date</code> here. We've purposefully omitted this from the F# model since we don't want the app to exude any control over this. But it will come in useful later for intraday sorting.</p>
</blockquote>
<p>Another thing we'll want to model is the state changes of our entry, which will take the form of either 1) creating a new entry, or 2) updating existing entry. I like this approach, despite it involving some duplicate code, because it uses the type system to make our intent clear and more importantly prevents us from filling meaningful fields with placeholder data to knowingly ignore.</p>
<pre><code class="language-fsharp">module FalcoJournal.Domain

open System

// ...

type NewEntry =
    { HtmlContent : string
      TextContent : string }

type UpdateEntry =
    { EntryId     : int
      HtmlContent : string
      TextContent : string }
</code></pre>
<h3 id="input-validation">Input Validation</h3>
<p>It's important to stop here for a minute and appreciate that these two entities represent points of input from the user. This means it would likely be wise of us to put some guards in place to ensure that data follows the shape we expect it to. To do this we'll run input validation against the raw input values, using <a href="https://github.com/pimbrouwers/Validus">Validus</a>.</p>
<p>Our goal will be to:</p>
<ul>
<li>Ensure the user can't submit an empty entry.</li>
<li>Ensure the user can't submit an entry with an invalid identifier.</li>
</ul>
<pre><code class="language-fsharp">module FalcoJournal.Domain

open System

// ...

type NewEntry =
    { HtmlContent : string
      TextContent : string }

    // equally valid to make this a module function
    static member Create html text : ValidationResult&lt;NewEntry&gt; =
        let htmlValidator : Validator&lt;string&gt; =
            // ensure the HTML is not empty, and check for empty &lt;li&gt;&lt;/li&gt;
            // which is the default value
            Validators.String.notEmpty None
            &lt;+&gt; Validators.String.notEquals &quot;&lt;li&gt;&lt;/li&gt;&quot; (Some (sprintf &quot;%s must not be empty&quot;))

        let create html text =
            { HtmlContent = html
              TextContent = text }

        create
        &lt;!&gt; htmlValidator &quot;HTML&quot; html
        &lt;*&gt; Validators.String.notEmpty None &quot;Text&quot; text

type UpdateEntry =
    { EntryId     : int
      HtmlContent : string
      TextContent : string }

    // equally valid to make this a module function
    static member Create entryId html text : ValidationResult&lt;UpdateEntry&gt; =
        let create (entryId : int) (entry : NewEntry) =
            { EntryId = entryId
              HtmlContent = entry.HtmlContent
              TextContent = entry.TextContent }

        // repurpose the validation from NewEntry, since it's shape
        // resembles this and also check that the EntryId is gt 0
        create
        &lt;!&gt; Validators.Int.greaterThan 0 (Some (sprintf &quot;Invalid %s&quot;)) &quot;Entry ID&quot; entryId
        &lt;*&gt; NewEntry.Create html text
</code></pre>
<p>Now, this adds a fair chunk of code to our previous slim domain. But I'll argue it's worthwhile it for it to live here on the basis that this code is highly cohesive with the type and may end up being used in more than one place.</p>
<p><a href="https://github.com/pimbrouwers/Validus">Validus</a> comes with validators for most primitives types, which reside in the <code>Validators</code> module. They all share a similar function definition, excluding config params. Using some loose F# pseudo-code:</p>
<pre><code class="language-fsharp">Validators.Int.greaterThan {minValue} {validation message}
// int -&gt; ValidationMessage option -&gt; Validator

ValidationMessage {fieldName}
// string -&gt; string

Validator {fieldName} {value}
// string -&gt; 'a -&gt; ValidationResult&lt;'a&gt;
</code></pre>
<p>Validators can be composed using the <code>&lt;+&gt;</code> operator, or <code>Validator.compose</code>. This is demonstrated above in the <code>let htmlValidator : Validator&lt;string&gt; = ...</code> which combines two string validators, including a custom message for the case of an empty <code>&lt;li&gt;&lt;/li&gt;</code>.</p>
<h2 id="ui">UI</h2>
<p>Next on our list are some of the UI elements that will be shared amongst our handlers, including things like buttons, form fields, the layout, top bar and error summary. We'll be using <a href="/2018/12/05/tachyons-the-best-library-you-re-not-using.html">tachyons</a> atomic CSS library since it's lovely for rapid UI development.</p>
<blockquote>
<p>Note that to save some space we won't be outlining all of the UI elements in the post. But the full source can be found in final <a href="https://github.com/pimbrouwers/FalcoJournal">repository</a>.</p>
</blockquote>
<h3 id="falco-view-engine">Falco View Engine</h3>
<p>A core feature of <a href="https://github.com/pimbrouwers/Falco">Falco</a> is the XML markup module. It can be used to produce any form of angle-bracket markup (i.e. HTML, SVG, XML etc.).</p>
<p>Most of the standard HTML tags &amp; attributes have been built into the markup module, which are pure functions composed into well-formed markup at run time. HTML tag functions are found in the <code>Elem</code> module, attributes in the <code>Attr</code> module. For string literal output there are several functions available in the <code>Text</code> module.</p>
<pre><code class="language-fsharp">Elem.div [ Attr.class' &quot;bg-red&quot; ] [ Text.rawf &quot;Hello %s&quot; &quot;world&quot; ]
</code></pre>
<p>Rumor has it that the <a href="https://github.com/pimbrouwers/Falco">Falco</a> creator makes a dog sound every time he uses <code>Text.rawf</code>.</p>
<blockquote>
<p><strong>Note</strong>: I am the creator, and this is entirely true.</p>
</blockquote>
<h3 id="extensible-ui">Extensible UI</h3>
<p>Idiomatic usage of the <code>Markup</code> module will involve building yourself a suite of project-specific functions for shared UI elements/sections/components. Doing this will drastically simplify the markup code in the feature layer(s).</p>
<p>To demonstrate how this might look, the code below shows some of the common UI elements in our app.</p>
<p>The important takeaway is how extensible having a view engine comprised of pure functions is. End-of-the-line elements, like the <code>pageTitle</code> below, drastically simplify the API for creating the specific chunk of markup. Wrapper elements, like the <code>topBar</code> below, show how elegantly we can compose complex view thanks to the recursive structure of the <code>XmlNode</code> type.</p>
<pre><code class="language-fsharp">module FalcoJournal.UI

open Falco.Markup

module Common =
    /// Display a list of errors as &lt;ul&gt;...&lt;/ul&gt;
    let errorSummary (errors : string list) =
        match errors.Length with
        | n when n &gt; 0 -&gt;
            Elem.ul [ Attr.class' &quot;mt0 pa2 pl4 red bg-washed-red ba br1 b--red&quot; ]
                    (errors |&gt; List.map (fun e -&gt; Elem.li [] [ Text.raw e ]))

        | _ -&gt;
            Elem.div [] []

    /// Page title as &lt;h1&gt;&lt;/h1&gt;
    let pageTitle (title : string) =
        Elem.h1 [ Attr.class' &quot;pb3 code tc white-90&quot; ] [ Text.raw title ]

    /// Top bar with pluggable actions
    let topBar (actions : XmlNode list) =
        Elem.header [ Attr.class' &quot;pv4&quot; ] [
            Elem.nav [ Attr.class' &quot;flex items-center&quot; ] [
                Elem.a [ Attr.class' &quot;f4 f3-l white-90 no-underline&quot;
                         Attr.href Urls.index ] [ Text.raw &quot;Falco Journal&quot; ]
                Elem.div [ Attr.class' &quot;flex-grow-1 tr&quot; ] actions ] ]
</code></pre>
<h2 id="data-access">Data Access</h2>
<p>We're going to build ourselves a simple, yet robust data access layer using <a href="https://github.com/pimbrouwers/Donald">Donald</a>. For the sake of simplicity, we'll be sticking to a synchronous approach here, which suits us well anyway given our use of SQLite.</p>
<p>The programming model for <a href="https://github.com/pimbrouwers/Donald">Donald</a> is simple, build a <code>IDbCommand</code> from a SQL statement and parameter set, and execute against either an <code>IDbConnection</code> or <code>IDbTransaction</code> (for transactional scenarios). Given that the operation is occurring against a medium with side effects, the return type is always of <code>DbResult&lt;'a&gt;</code>.</p>
<p>To make the development process easier, I find it very useful to devise a &quot;log and pass-through&quot; function for <code>DbResult&lt;'a&gt;</code> (which conveniently returns the full statement and exception thrown):</p>
<blockquote>
<p><strong>Fun fact</strong>: Unlike ADO.NET, <a href="https://github.com/pimbrouwers/Donald">Donald</a> will return field-specific errors when they occur. Providing not only the full statement, but also the specific field which caused the error.</p>
</blockquote>
<pre><code class="language-fsharp">module FalcoJournal.Provider

open System.Data
open Donald
open Microsoft.Extensions.Logging
open FalcoJournal.Domain

// ...

module private DbResult =
    /// Log DbResult, if error, and return
    let logError (log : ILogger) (dbResult : DbResult&lt;'a&gt;) : DbResult&lt;'a&gt; =
        match dbResult with
        | Ok _     -&gt; dbResult
        | Error ex -&gt;
            log.LogError(ex.Error, sprintf &quot;DB ERROR: Failed to execute\n%s&quot; ex.Statement)
            dbResult

// ...
</code></pre>
<p>With this in place, we're ready to write some data access code. Again, to save time and space only a single example will be shown here. But the full source can be found in the <a href="https://github.com/pimbrouwers/FalcoJournal">repository</a>.</p>
<pre><code class="language-fsharp">module FalcoJournal.Provider

open System.Data
open Donald
open Microsoft.Extensions.Logging
open FalcoJournal.Domain

// ...

module EntryProvider =
    type Get = int -&gt; DbResult&lt;Entry option&gt;

    let get (log : ILogger) (conn : IDbConnection) =
        fun entryId -&gt;
            let sql = &quot;
            SELECT    entry_id
                    , html_content
                    , text_content
                    , entry_date
            FROM      entry
            WHERE     entry_id = @entry_id&quot;

            let fromDataReader (rd : IDataReader) : Entry =
                { EntryId      = rd.ReadInt32 &quot;entry_id&quot;
                  HtmlContent  = rd.ReadString &quot;html_content&quot;
                  TextContent  = rd.ReadString &quot;text_content&quot;
                  EntryDate    = rd.ReadDateTime &quot;entry_date&quot; }

            dbCommand conn {
                cmdText  sql
                cmdParam [ &quot;entry_id&quot;, SqlType.Int32 entryId ]
            }
            |&gt; DbConn.querySingle fromDataReader
            |&gt; DbResult.logError log
    // ...
</code></pre>
<p>Nothing crazy going on here, but a couple of interesting points to discuss. Firstly, you'll notice that we've specified a function type to represent the operation. I enjoy the ergonomics of this approach, since it ultimately becomes the interface between the IO and business layer's.</p>
<p>The other point of note is that the function type contains no trace of the logger or database connection. So why is that? Since these are not concretely required to <em>perform</em> the operation, they aren't the concern of our final consumers. But they're still required, so how to we fulfill this dependency? Partial application of course! And for that, we'll use a <a href="#the-composition-root"><strong>composition root</strong></a> to ensure we aren't having to manually manifest these depdendencies each time we want to interact with the database.</p>
<h3 id="a-brief-tangent-on-database-connections">A brief tangent on database connections</h3>
<p>My preference for database-bound projects is to only specify how to create new <code>IDbConnection</code> instances in one place. So for that, we'll create a type to represent our <strong>connection factory</strong>.</p>
<pre><code class="language-fsharp">type DbConnectionFactory = unit -&gt; IDbConnection
</code></pre>
<p>And we'll register an instance of this within our service collection in <code>Program.fs</code>:</p>
<pre><code class="language-fsharp">module Falco.Program

// ------------
// Register services
// ------------
let configureServices (connectionFactory : DbConnectionFactory) (services : IServiceCollection) =
    services.AddSingleton&lt;DbConnectionFactory&gt;(connectionFactory)
    // ...

// -----------
// Configure Web host
// -----------
let configureWebHost (endpoints : HttpEndpoint list) (webHost : IWebHostBuilder) =
    // ...
    let connectionString = appConfiguration.GetConnectionString(&quot;Default&quot;)
    let connectionFactory () = new SQLiteConnection(connectionString, true) :&gt; IDbConnection
    // ...

</code></pre>
<h2 id="the-composition-root">The composition root</h2>
<p>Now this next little bit is completely optional, you can very effectively build Falco apps without doing this. But I love this approach because it gets me away from having to do the lambda-style <code>HttpHandler</code> every time I need access to dependencies, and almost becomes a tiny DSL for doing your business-logic that automatically enriches your IO layer with it's dependencies.</p>
<p>Since we're now in the web-tier so to speak, we can begin to leverage the composable <code>HttpHandler</code> type. In combination with a very simple function definition, we can save ourselves a TON of code repetition in our feature layer. The best part? All of this amazing functionality only costs you 29 lines of code, with comments!</p>
<pre><code class="language-fsharp">module FalcoJournal.Service

open System.Data
open Falco
open Microsoft.Extensions.Logging
open FalcoJournal.Provider

/// Work to be done that has input and will generate output
type ServiceHandler&lt;'input, 'output, 'error&gt; = 'input -&gt; Result&lt;'output, 'error&gt;

/// An HttpHandler to execute services, and can help reduce code
/// repetition by acting as a composition root for injecting
/// dependencies for logging, database, http etc.
let run
    (serviceHandler: ILogger -&gt; IDbConnection -&gt; ServiceHandler&lt;'input, 'output, 'error&gt;)
    (handleOk : 'output -&gt; HttpHandler)
    (handleError : 'input -&gt; 'error -&gt; HttpHandler)
    (input : 'input) : HttpHandler =
    fun ctx -&gt;
        let connectionFactory = ctx.GetService&lt;DbConnectionFactory&gt;()
        use connection = connectionFactory ()
        let log = ctx.GetLogger &quot;FalcoJournal.Service&quot;

        let respondWith =
            match serviceHandler log connection input with
            | Ok output -&gt; handleOk output
            | Error error -&gt; handleError input error

        respondWith ctx
</code></pre>
<p>In a nutshell, we define a function type to represent a &quot;service&quot; (i.e. the work to be done) which effectively says &quot;given an input you will either receive a successful result containing output, or an error&quot;. It's basic, but powerful. Next, we create an <code>HttpHandler</code> which will manifest our dependencies, inject them into the provided service and based on the response type invoke either a <em>success</em> or <em>failure</em> handler.</p>
<p>With this in place, our final handlers will look like this, instead of each containing the code above.</p>
<pre><code class="language-fsharp">let handle : HttpHandler =
    let handleError input error : HttpHandler = // ...

    let handleOk output  : HttpHandler = // ...

    let workflow log conn input : ServiceHandler&lt;unit, Output, Error&gt; = // ...

    Service.run workflow handleOk handleError ()
</code></pre>
<h2 id="entry-feature">Entry Feature</h2>
<p>Still with me? I realize it's been a lot of content to get here, but it's time for the raison d'être. Our app will be responsible for 3 primary actions:</p>
<ol>
<li><p>List all journal entries by date descending - <code>GET /</code></p>
</li>
<li><p>Persistently create a journal entry - <code>GET + POST /entry/create</code></p>
</li>
<li><p>Persistently update a journal entry - <code>GET + POST /entry/edit/{id}</code></p>
</li>
</ol>
<p>To do this, we're going to use a <strong>feature-based</strong> approach and encapsulate each action into it's own module with roughly the following shape:</p>
<pre><code class="language-fsharp">module FalcoJournal.Entry

module Create =
    type Input = // ... (optional)

    type Error = // ...

    let service : ServiceHandler&lt;'input, 'output, Error&gt; = // ...

    let view : XmlNode = // ...

    let handle : HttpHandler = // ...
</code></pre>
<p>For this post, we'll be covering #2, creating a new journal entry. But, feel free to checkout the <a href="https://github.com/pimbrouwers/FalcoJournal">full source</a> for the other two endpoints.</p>
<h3 id="creating-a-new-journal-entry">Creating a new journal entry</h3>
<p>When building server-side MPA's (multi-page apps) most actions will come in pairs. A <code>GET</code> to load the initial UI and data, and a <code>POST</code> (or <code>PUT</code>) to handle submissions. Our case will be no exception. First we'll devise the <code>GET</code> handler and a view which will be shared between the pair. Then we'll move on to build the supporting <code>POST</code> action.</p>
<h4 id="get-entrycreate"><code>GET /entry/create</code></h4>
<p>For this endpoint we know we need a view, and a means of rendering it. Luckily we've already got a working suite of UI utilities to pull from, and Falco comes with everything we need to render it.</p>
<pre><code class="language-fsharp">open Falco.JournalEntry

// ...

module New =
    let view (errors : string list) (newEntry : NewEntryModel) =
        let title = &quot;A place for your thoughts...&quot;

        let actions = [
            Forms.submit [ Attr.value &quot;Save Entry&quot;; Attr.form &quot;entry-editor&quot; ]
            Buttons.solidWhite &quot;Cancel&quot; Urls.index ]

        let editorContent =
            match newEntry.HtmlContent with
            | str when StringUtils.strEmpty str -&gt;
                Elem.li [] []

            | _ -&gt;
                Text.raw newEntry.HtmlContent

        Layouts.master title [
            Common.topBar actions

            Common.errorSummary errors

            Elem.form [ Attr.id &quot;entry-editor&quot;
                        Attr.method &quot;post&quot;
                        Attr.action Urls.entryCreate ] [

                Elem.ul [ Attr.id &quot;bullet-editor&quot;
                        Attr.class' &quot;mh0 pl3 outline-0&quot;
                        Attr.create &quot;contenteditable&quot; &quot;true&quot; ] [

                    editorContent
                ]

                Elem.input [ Attr.id &quot;bullet-editor-html&quot;
                            Attr.type' &quot;hidden&quot;
                            Attr.name &quot;html_content&quot; ]

                Elem.input [ Attr.id &quot;bullet-editor-text&quot;
                            Attr.type' &quot;hidden&quot;
                            Attr.name &quot;text_content&quot; ]
            ]
        ]

    let handle : HttpHandler =
        view [] NewEntryModel.Empty
        |&gt; Response.ofHtml
</code></pre>
<p>Some of this should already be familiar from our discussion in the <a href="#ui">UI</a> section, specifically the <code>Common.topBar</code> and <code>Common.errorSummary</code>. We've also introduced a few other UI elements, most notably our master layout (<code>Layouts.master</code>) and button elements (<code>Button.solidWhite</code>).</p>
<p>You'll notice that our view function takes a <code>NewEntryModel</code> as it's final input parameter. This leads into one of the main benefits of using the <a href="https://github.com/pimbrouwers/Falco">Falco</a> markup module as an HTML view engine, strong typing. If the definition of <code>NewEntryModel</code> (seen below) changes in a breaking way, you better believe the compiler will tell you about it, and this is a great thing. Just think of all the bugs this can save!</p>
<p>The actual handler in this case is pretty straight-forward. It simply invokes the view with an empty error list and empty model, and passes it into the <a href="https://github.com/pimbrouwers/Falco">Falco</a> <code>Response.ofHtml</code> handler.</p>
<blockquote>
<p>Defining a static member for creating an empty record like this, <code>NewEntryModel.Empty</code>, is a pattern I like to keep my code clutter free.</p>
</blockquote>
<h3 id="post-entrycreate"><code>POST /entry/create</code></h3>
<p>The <code>POST</code> aspect of our pairing is a little more interesting, requiring us to define error states, a service and wire up our dependencies. In order to obtain the user input, we'll also need to model bind the form values submitted and validate them to ensure they meet our criteria. And in the case of submission errors, we'll render the view from the <code>GET</code> action of the pair.</p>
<pre><code class="language-fsharp">type NewEntryModel =
    { HtmlContent : string
      TextContent : string }

    static member Create html text =
        { HtmlContent = html
          TextContent = text }

    static member Empty =
        NewEntryModel.Create String.Empty String.Empty

module Create =
    type Error =
        | InvalidInput of string list
        | UnexpectedError

    let service (createEntry : EntryProvider.Create) : ServiceHandler&lt;NewEntryModel, unit, Error&gt; =
        let validateInput (input : NewEntryModel) : Result&lt;NewEntry, Error&gt; =
            let result = NewEntry.Create input.HtmlContent input.TextContent

            match result with
            | Success newEntry -&gt; Ok newEntry
            | Failure errors   -&gt;
                errors
                |&gt; ValidationErrors.toList
                |&gt; InvalidInput
                |&gt; Error

        let commitEntry entry : Result&lt;unit, Error&gt; =
            match createEntry entry with
            | Error e -&gt; Error UnexpectedError
            | Ok ()   -&gt; Ok ()

        fun input -&gt;
            input
            |&gt; validateInput
            |&gt; Result.bind commitEntry

    let handle : HttpHandler =
        let handleError (input : NewEntryModel) (error : Error) =
            let errorMessages =
                match error with
                | UnexpectedError     -&gt; [ &quot;Something went wrong&quot; ]
                | InvalidInput errors -&gt; errors

            New.view errorMessages input
            |&gt; Response.ofHtml

        let handleOk () : HttpHandler =
            Response.redirect Urls.index false

        let workflow (log : ILogger) (conn : IDbConnection) (input : NewEntryModel) =
            service (EntryProvider.create log conn) input

        let formMap (form : FormCollectionReader) : NewEntryModel =
            { HtmlContent = form.GetString &quot;html_content&quot; &quot;&quot;
              TextContent = form.GetString &quot;text_content&quot; &quot;&quot; }

        Request.mapForm
            formMap
            (Service.run workflow handleOk handleError)
</code></pre>
<p>Following our pattern from above, the module tells a nice &quot;story&quot; about how it's little world works. We immediately see that we can expect one of two error states, invalid input or an unexpected error (kind of an &quot;error bucket&quot; I understand, but this is a demo app). Next we can see that there will be work potentially done, that requires a <code>NewEntryModel</code> to come in and will either produce no output, <code>unit</code> in the case of success, or an <code>Error</code> in the case of failure which we are forced to deal with thanks to F# completeness.</p>
<p>The service itself defines a single dependency, on <code>EntryProvider.Create</code>, which has been partially applied in our composition root with the <code>ILogger</code> and <code>IDbConnection</code> it requires to perform IO. Two discrete steps are laid out in the service, validating the input &amp; transforming it into our valid domain model, <code>NewEntry</code>, and committing the entry to our database. Finally, we define a simple result-based pipeline to process our input.</p>
<p>The form of the <code>HttpHandler</code> also tells a story that is easy to reason about. If we hide the definitions of the four functions, we can easily see that we have code to: handle errors, handle success, define our workflow and map our form. And truly there isn't much more to it than that.</p>
<p>We kick things off using another function built into Falco called <code>Request.mapForm</code> which takes a mapping function (<code>FormCollectionReader -&gt; 'a</code>) as it's first parameter and an input-bound HttpHandler (<code>'a -&gt; HttpHandler</code>) as a second parameter. In our case, this handler is the function we defined in our composition root, which requires input of type <code>'a</code> for a service of type <code>ServiceHandler&lt;'a, 'output, 'error&gt;</code>. From here, if the service succeeds, <code>handleOk</code> response by redirecting us back to <code>/</code> and if the service fails we are shown the view with error messages displayed.</p>
<blockquote>
<p><strong>Did you know?</strong> <br/>Similar to the <code>FormCollectionReader</code>, <a href="https://github.com/pimbrouwers/Falco">Falco</a> offers analogous readers for: <a href="https://github.com/pimbrouwers/Falco#query-binding">query strings</a>, headers and <a href="https://github.com/pimbrouwers/Falco#route-binding">route values</a>. All of which allow you to safely and reliably obtain values from these disparate sources in a uniform way.</p>
</blockquote>
<h2 id="in-conclusion">In conclusion</h2>
<p>This post was a ton of fun to write, and yet so difficult to decide upon what content made it in and what didn't. Hopefully my decision making there was on point and this point helps paint a clear picture about developing idiomatic F# web apps using Falco.</p>
<p>If you've used Falco and love it (or hate it) I want to <a href="https://github.com/pimbrouwers/Falco/issues">hear</a> from you! I'm usually also available on both Slack and <a href="https://twitter.com/falco_framework">Twitter</a> if you'd prefer to reach me there.</p>


<!-- /body/footer -->
<hr>
<footer>
  <p>
    <div>Pim Brouwers</div>
    <div>
      <a href="//github.com/pimbrouwers" target="_blank">GitHub</a>
    </div>
  </p>
</footer>

<script src="/prism.js"></script>
</body>
</html>


